---
title: Application.Run method (Excel)
keywords: vbaxl10.chm132104
f1_keywords:
- vbaxl10.chm132104
api_name:
- Excel.Application.Run
ms.assetid: 3e0167ab-b101-018f-0f89-ada116b8bb72
ms.date: 10/06/2026
ms.localizationpriority: medium
---


# Application.Run method (Excel)

Runs a macro or calls a function. This can be used to run a macro written in Visual Basic or the Microsoft Excel macro language, or to run a function in a DLL or XLL.


## Syntax

_expression_.**Run** (_Macro_, _Arg1_, _Arg2_, _Arg3_, _Arg4_, _Arg5_, _Arg6_, _Arg7_, _Arg8_, _Arg9_, _Arg10_, _Arg11_, _Arg12_, _Arg13_, _Arg14_, _Arg15_, _Arg16_, _Arg17_, _Arg18_, _Arg19_, _Arg20_, _Arg21_, _Arg22_, _Arg23_, _Arg24_, _Arg25_, _Arg26_, _Arg27_, _Arg28_, _Arg29_, _Arg30_)

_expression_ A variable that represents an **[Application](Excel.Application(object).md)** object.


## Parameters

|Name|Required/Optional|Data type|Description|
|:-----|:-----|:-----|:-----|
| _Macro_|Optional| **Variant**|The macro to run.<br/><br/>This can be either a string with the macro name, a **[Range](Excel.Range(object).md)** object indicating where the function is, or a register ID for a registered DLL (XLL) function.<br/><br/>If a string is used, the string will be evaluated in the context of the active sheet.|
| _Arg1_&ndash;_Arg30_|Optional| **Variant**|An argument that should be passed to the function.|

## Return value

Variant


## Remarks

You cannot use named arguments with this method. Arguments must be passed by position.

The **Run** method returns whatever the called macro returns.

### Error handling

If the called macro raises a run-time error that it doesn't handle (for example, an error raised by the **[Err.Raise](../Language/Reference/User-Interface-Help/raise-method.md)** method, or a division by zero), the error isn't passed to the procedure that calls **Run**. Excel displays the run-time error dialog box at that point, even if the calling procedure has an enabled error handler set with **[On Error GoTo](../Language/Reference/User-Interface-Help/on-error-statement.md)**. The dialog box is displayed even when Excel isn't visible, so unattended code stops until someone responds to it. If the user chooses **End**, all running macros stop, including the calling procedure. When **Run** is called through Automation, the client then receives a generic error instead of the original error number and description.

Errors that the **Run** method itself raises, such as the error that occurs when the specified macro can't be found, are passed to the calling procedure and can be handled by its error handler.

As with an ordinary procedure call, an **[End](../Language/Reference/User-Interface-Help/end-statement.md)** statement in the called macro stops all running macros, including the calling procedure, so the statement after **Run** isn't executed. Module-level variables are reset both in the workbook that contains the called macro and in the workbook of the calling procedure.

Changes that the called macro makes to a **ByRef** argument aren't returned to the calling procedure. To report a failure to the caller, handle errors inside the called macro and return a value that indicates success or failure, as shown in the following example.


## Example

In this example, the **ImportData** function in the Tools.xlsm workbook handles its own errors and returns **False** if it fails. The calling procedure, in another workbook, checks the return value instead of relying on its own error handler. Tools.xlsm must be open when **Run** is called.

```vb
' In Tools.xlsm. Called through Application.Run
Public Function ImportData(ByVal filePath As String) As Boolean
    On Error GoTo ErrorHandler
    Workbooks.Open filePath
    ' ... process the data ...
    ImportData = True
    Exit Function
ErrorHandler:
    Debug.Print "ImportData failed: " & Err.Number & " " & Err.Description
    ImportData = False
End Function

' In the calling workbook
Sub CallImportData()
    If Not Application.Run("'Tools.xlsm'!ImportData", "C:\Data\input.xlsx") Then
        MsgBox "The import failed."
    End If
End Sub
```




[!include[Support and feedback](~/includes/feedback-boilerplate.md)]
